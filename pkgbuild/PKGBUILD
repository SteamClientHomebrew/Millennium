pkgver=2.25.1.r14.g013763a
pkgname="millennium"
pkgrel=1
pkgdesc="Millennium is an open-source low-code modding framework to create, manage and use themes/plugins for the desktop Steam Client without any low-level internal interaction or overhead."
arch=('x86_64')
url="https://github.com/SteamClientHomebrew/Millennium"
license=('MIT')
depends=('git' 'steam')
makedepends=('npm' 'curl' 'zip' 'unzip' 'tar' 'cmake' 'ninja' 'lib32-gcc-libs')
depends_x86_64=('python-i686-bin')
conflicts=("$_pkgname")
provides=("$_pkgname")
source=("git+$url.git")
sha256sums=('SKIP')
validpgpkeys=('D4A49B8AB39D704F')
options=(!debug)
install=millennium.install

prepare() {
    cd "millennium"
    echo -e "\e[1m\e[92m==>\e[0m \e[1mCloning submodules...\e[0m"
    git submodule update --init --recursive
}

pkgver() {
    cd "millennium"
    git describe --long --tags --abbrev=7 | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
    export NODE_NO_WARNINGS=1
    cd "$srcdir/millennium"

    echo -e "\e[1m\e[92m==>\e[0m \e[1mBuilding Millennium core assets...\e[0m"

    cd assets
    npm install @steambrew/api --silent
    npm install --silent
    npm run build
    cd ..

    echo -e "\e[1m\e[92m==>\e[0m \e[1mBootstrapping VCPKG...\e[0m"

    ./vendor/vcpkg/bootstrap-vcpkg.sh
    ./vendor/vcpkg/vcpkg integrate install

    echo -e "\e[1m\e[92m==>\e[0m \e[1mBuilding Millennium...\e[0m"

    cmake --preset=linux-release -G "Ninja" -DDISTRO_ARCH=ON
    cmake --build build --config Release
}

package() {
    cd "$srcdir/millennium"
    mkdir -p "$pkgdir/usr/share/millennium/assets/.millennium/Dist"

    cp -r ./assets/.millennium/Dist/index.js "$pkgdir/usr/share/millennium/assets/.millennium/Dist/index.js"
    cp -r ./assets/core "$pkgdir/usr/share/millennium/assets/core"
    cp -r ./assets/pipx "$pkgdir/usr/share/millennium/assets/pipx"
    cp -r ./assets/requirements.txt "$pkgdir/usr/share/millennium/assets/requirements.txt"
    cp -r ./assets/plugin.json "$pkgdir/usr/share/millennium/assets/plugin.json"
    
    mkdir -p "$pkgdir/usr/lib/millennium"

    install -Dm755 build/libmillennium_x86.so "$pkgdir/usr/lib/millennium/libmillennium_x86.so"
    install -Dm755 build/cli/millennium "$pkgdir/usr/bin/millennium"
    
    mkdir -p "$pkgdir/usr/share/millennium/shims"

    cp -r ./shims/build/* "$pkgdir/usr/share/millennium/shims/"

    install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
