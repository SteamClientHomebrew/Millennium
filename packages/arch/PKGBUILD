# Maintainers: SteamClientHomebrew <https://github.com/SteamClientHomebrew>

pkgver=2.31.0
pkgname="millennium"
pkgrel=2
pkgdesc="Millennium is an open-source low-code modding framework to create, manage and use themes/plugins for the desktop Steam Client without any low-level internal interaction or overhead."
arch=('x86_64')
url="https://github.com/SteamClientHomebrew/Millennium"
license=('MIT')
depends=('git' 'steam')
makedepends=('npm' 'curl' 'zip' 'unzip' 'tar' 'cmake' 'ninja' 'lib32-gcc-libs' 'pnpm')
depends_x86_64=('lib32-python311-bin')
conflicts=('python-i686-bin')
source=("git+$url.git#branch=next") # TODO: update to commit on main branch when we merge.
sha256sums=('SKIP')
options=(!debug)
install=millennium.install

prepare() {
    cd "$srcdir/$_pkgdir" || exit
    echo -e "\e[1m\e[92m==>\e[0m \e[1mCloning submodules...\e[0m"
    git submodule update --init --recursive
}

build() {
    export NODE_NO_WARNINGS=1
    cd "$srcdir/$_pkgdir" || exit

    echo -e "\e[1m\e[92m==>\e[0m \e[1mBuilding Millennium assets...\e[0m"

    pnpm --dir src/frontend install
    pnpm --dir src/frontend run build
    pnpm --dir sdk          install
    pnpm --dir sdk          run build

    mkdir -p    "./shims/build/"
    cp -r       "./sdk/typescript-packages/loader/build/*" "./shims/build/"

    echo -e "\e[1m\e[92m==>\e[0m \e[1mBuilding Millennium...\e[0m"

    cmake --preset=linux-release -G "Ninja" -DDISTRO_ARCH=ON
    cmake --build build --config Release
}

package() {
    cd "$srcdir/$_pkgdir" || exit

    # Create final directory structure
    mkdir -p "$pkgdir/usr/lib/millennium"
    mkdir -p "$pkgdir/usr/share/millennium/shims"
    mkdir -p "$pkgdir/usr/share/millennium/assets"

    install -Dm755  "build/libmillennium_x86.so"                        "$pkgdir/usr/lib/millennium/libmillennium_x86.so"
    install -Dm755  "build/unix-hooks/libmillennium_bootstrap_86x.so"   "$pkgdir/usr/lib/millennium/libmillennium_bootstrap_86x.so"
    install -Dm755  "build/src/hhx64-build/libmillennium_hhx64.so"      "$pkgdir/usr/lib/millennium/libmillennium_hhx64.so"
    mv              "src/pipx"                                          "$pkgdir/usr/share/millennium/assets/"
    cp -r           "./shims/build/*"                                   "$pkgdir/usr/share/millennium/shims/"
    install -Dm644  "LICENSE"                                           "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
